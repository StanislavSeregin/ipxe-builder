#!ipxe

set win11-work iscsi:10.0.0.1::::iqn.2003-01.org.linux-iscsi.stas-server.x8664:sn.d907af099920
set win11-home iscsi:10.0.0.1::::iqn.2003-01.org.linux-iscsi.stas-server.x8664:sn.19f9a5f0cc98
set ubuntu iscsi:10.0.0.1::::iqn.2003-01.org.linux-iscsi.stas-server.x8664:sn.c727f9503423

menu Boot Menu
item --gap -- iSCSI targets:
item entry1 Windows 11 (Work)
item entry2 Windows 11 (Home safe)
item entry3 Ubuntu (QEMU KVM)
item --gap -- Utils:
item entry4 WinPE 10
item entry5 WinPE 11
choose os

dhcp
set gateway 0.0.0.0
set initiator-iqn iqn.1991-05.com.microsoft:stas

goto ${os}

:entry1
sanboot --keep --drive 0x80 ${win11-work}

:entry2
sanboot --keep --drive 0x80 ${win11-home}

:entry3
sanboot --filename \EFI\ubuntu\shimx64.efi ${ubuntu}

:entry4
goto hook-menu
:entry4_1
kernel wimboot gui
initrd -n bootx64.efi bootx64.efi bootx64.efi
initrd -n bcd BCD bcd
initrd -n boot.sdi boot.sdi boot.sdi
initrd -n boot.wim strelec10x64.wim boot.wim
boot

:entry5
goto hook-menu
:entry5_1
kernel wimboot gui
initrd -n bootx64.efi bootx64.efi bootx64.efi
initrd -n bcd BCD bcd
initrd -n boot.sdi boot.sdi boot.sdi
initrd -n boot.wim strelec11x64.wim boot.wim
boot

:hook-menu
menu Hook iSCSI
item hook0 Nothing
item hook1 Windows 11 (Work)
item hook2 Windows 11 (Home safe)
choose hook-entry
goto ${hook-entry}

:hook0
goto ${os}_1

:hook1
sanhook --drive 0x80 ${win11-work}
goto ${os}_1

:hook2
sanhook --drive 0x80 ${win11-home}
goto ${os}_1
